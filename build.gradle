group 'com.github.bpazy'
version '0.2.1'

apply plugin: "maven-publish"
apply plugin: 'com.jfrog.bintray'
apply plugin: 'java'

sourceCompatibility = 1.8

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "org.projectlombok:lombok:1.16.16"

    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.1'
    compile group: 'com.google.guava', name: 'guava', version: '21.0'
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Version': version,
                'Main-Class': 'Main'
    }
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: classes) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "Bpazy"
            name "ziyuan"
            email "hanziyuan08@outlook.com"
        }
    }
    scm {
        url "https://github.com/Bpazy/cqjavaapi"
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'com.github.bpazy'
            artifactId 'cqjavaapi'
            version project.version

            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'cq java api base on cqsockerapi')
                root.appendNode('name', 'cqjavaapi')
                root.appendNode('url', 'https://github.com/Bpazy/cqjavaapi')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    pkg {
        repo = 'maven'
        name = 'cqjavaapi'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Bpazy/cqjavaapi.git'
    }
}
